#!/usr/bin/perl -w
# 
# LICENSE
# Copyright (c) 2002-2003 Matthias Trautner Kromann <mtk@id.cbs.dk>
# 
# The code in this package is free software: You can redistribute it
# and/or modify it under the terms of the GNU General Public License 
# published by the Free Software Foundation. This package is
# distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY or any implied warranty of MERCHANTABILITY or FITNESS FOR
# A PARTICULAR PURPOSE. See the GNU General Public License for more
# details. 
# 
# The GNU General Public License is contained in the file LICENSE-GPL.
# Please consult the DTAG homepages for more information about DTAG:
#
#     http://sf.net/projects/disgram/
#     http://www.id.cbs.dk/~mtk/dtag
# 
# Matthias Trautner Kromann
# mtk@id.cbs.dk
#

#!/usr/bin/perl -w

# Version
my $RELEASE = '0.8.7 (2008-05-30 15:33:45)';

use strict;
use File::Basename;
use Cwd;

# Find DTAGHOME
my $DTAGHOME = $ENV{'DTAGHOME'} || "";
if (! ($DTAGHOME && -r $DTAGHOME)) {
	# Find directory path to this script
	my $script = $0;

	# Resolve symbolic links
	while (-l $script) {
		my $linkfile = readlink($script);
		if ($linkfile !~ /^\//) {
			$linkfile = dirname($script) . "/" . $linkfile;
		}
		$script = $linkfile;
	}

	# Find absolute path and save in DTAGHOME
	$DTAGHOME = Cwd::abs_path(dirname($script)) if (-r $script) || "";
}

# Add $DTAGHOME to @INC, and load modules
push @INC, $DTAGHOME;
require DTAG::Graph;
require DTAG::Interpreter;
require DTAG::Lexicon;
require DTAG::LexInput;
require DTAG::Parse;
require DTAG::Learner;
require DTAG::Alignment;
require DTAG::ALexicon;

# Create new interpreter object and setup signal handling
my $inter = DTAG::Interpreter->new();
sub catch_signal {
	my $signame = shift;
	$inter->signal_handler($signame);
}
$SIG{INT} = \&catch_signal;

# Process options -q, -i, and -u
my $quiet = 0;
my $init = 1;
$inter->unsafe(1);
foreach my $arg (@ARGV) {
	$quiet = 1 if ($arg eq "-q");
	$init = 0 if ($arg eq "-i");
	$inter->unsafe(0) if ($arg eq "-s");
}

# Print copyright information
$inter->quiet($quiet);
if (! $quiet) {
	print "Welcome to DTAG dependency tagger v. $RELEASE\n";
	print "Copyright (c) 2002-2008 Matthias Buch-Kromann\n";
	print "Using DTAG directory $DTAGHOME\n";
}

# Process rc-file
my $rcfile = "";
$rcfile = $DTAGHOME . "/.dtagrc" 
	if ($DTAGHOME && -r $DTAGHOME . "/.dtagrc");
$rcfile = $ENV{'HOME'} . "/.dtagrc"
	if ($ENV{'HOME'} && -r $ENV{'HOME'} . "/.dtagrc");
$rcfile = ".dtagrc" 
	if (-r ".dtagrc");
$inter->quiet(1);
if ($rcfile && $init) {
	print "Using rcfile $rcfile\n" if (! $quiet);
	$inter->do("script $rcfile");
} 
$inter->quiet($quiet);

# Process command line arguments
while (@ARGV) {
	my $arg = shift(@ARGV);
	if ($arg eq "-s") {
		$inter->do("script " . shift(@ARGV));
	} elsif ($arg eq "-e") {
		$inter->do(shift(@ARGV), 1);
	} elsif ($arg eq "-E") {
		$inter->do(shift(@ARGV));
	}
}

# Interpret input until "exit"
$inter->loop();

